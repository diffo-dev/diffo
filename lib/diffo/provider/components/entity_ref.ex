defmodule Diffo.Provider.EntityRef do
  @moduledoc """
  Diffo - TMF Service and Resource Management with a difference

  EntityRef - Ash Resource for a TMF Entity Reference
  """
  use Ash.Resource,
    otp_app: :diffo,
    domain: Diffo.Provider,
    data_layer: AshNeo4j.DataLayer,
    extensions: [AshOutstanding.Resource, AshJason.Resource]

  resource do
    description "An Ash Resource for a TMF Entity Reference"
    plural_name :entityRefs
  end

  neo4j do
    relate([
      {:instance, :RELATES, :incoming, :Instance},
      {:entity, :RELATES, :outgoing, :Entity}
    ])
  end

  jason do
    pick([:role, :entity])

    customize(fn result, _record ->
      result
      |> Diffo.Util.extract_suppress(:entity, :id, :id)
      |> Diffo.Util.extract_suppress(:entity, :href, :href)
      |> Diffo.Util.extract_suppress(:entity, :name, :name)
      |> Diffo.Util.extract_suppress(:entity, :referredType, "@referredType")
      |> Diffo.Util.extract_suppress(:entity, :type, "@type")
      |> Diffo.Util.remove(:party)
    end)

    order([:id, :href, :name, :role, "@referredType", "@type"])
  end

  outstanding do
    expect([:role, :entity])
  end

  attributes do
    uuid_primary_key :id do
      description "a uuid4, unique to this entity ref, generated by default"
      public? false
    end

    attribute :role, :atom do
      description "the role of the entity to the instance"
      allow_nil? true
      public? true
    end

    create_timestamp :inserted_at

    update_timestamp :updated_at
  end

  relationships do
    belongs_to :instance, Diffo.Provider.Instance do
      description "the instance which relates to an entity via this entity ref"
      allow_nil? false
      public? true
    end

    belongs_to :entity, Diffo.Provider.Entity do
      description "the entity which relates from an instance via this entity ref"
      attribute_type :string
      allow_nil? false
      public? true
    end
  end

  actions do
    defaults [:read, :destroy]

    create :create do
      description "creates a entity ref, relating an instance and entity"
      accept [:role]
      argument :instance_id, :uuid
      argument :entity_id, :string

      change manage_relationship(:instance_id, :instance, type: :append_and_remove)
      change manage_relationship(:entity_id, :entity, type: :append_and_remove)
      change load [:entity]
    end

    read :list do
      description "lists all entity refs"
    end

    read :list_entity_refs_by_instance_id do
      description "lists entity refs by instance id"
      argument :instance_id, :uuid
      filter expr(instance_id == ^arg(:instance_id))
    end

    read :list_entity_refs_by_entity_id do
      description "lists entity refs by entity id"
      argument :entity_id, :string
      filter expr(entity_id == ^arg(:entity_id))
    end

    update :update do
      description "updates the entity ref role"
      accept [:role]
    end
  end

  identities do
    identity :instance_entity_uniqueness, [:instance_id, :entity_id] do
      message "another entity ref exists"
    end
  end

  preparations do
    prepare build(load: [:entity], sort: [inserted_at: :desc])
  end

  @doc """
  Compares two entity ref, by ascending entity_id
  ## Examples
    iex> Diffo.Provider.EntityRef.compare(%{entity_id: "a"}, %{entity_id: "a"})
    :eq
    iex> Diffo.Provider.EntityRef.compare(%{entity_id: "b"}, %{entity_id: "a"})
    :gt
    iex> Diffo.Provider.EntityRef.compare(%{entity_id: "a"}, %{entity_id: "b"})
    :lt

  """
  def compare(%{entity_id: entity_id0}, %{entity_id: entity_id1}),
    do: Diffo.Util.compare(entity_id0, entity_id1)
end
