defmodule Diffo.Provider.Characteristic do
  @moduledoc """
  Diffo - TMF Service and Resource Management with a difference
  Copyright Matt Beanland beanland@live.com.au

  Characteristic - Ash Resource for a TMF Characteristic
  """
  use Ash.Resource,
    otp_app: :diffo,
    domain: Diffo.Provider,
    data_layer: AshNeo4j.DataLayer,
    extensions: [AshOutstanding.Resource, AshJason.Resource]

  neo4j do
    label(:Characteristic)

    relate([
      {:instance, :DEFINES, :outgoing},
      {:feature, :DEFINES, :outgoing},
      {:relationship, :DEFINES, :outgoing}
    ])

    translate(id: :uuid)
  end

  jason do
    pick([:name, :value])
  end

  outstanding do
    expect([:name, :value])
  end

  attributes do
    uuid_primary_key :id do
      description "a uuid4, unique to this characteristic, generated by default"
      public? false
    end

    attribute :name, :atom do
      description "the name of this characteristic"
      allow_nil? false
      public? true
    end

    attribute :value, :term do
      description "the value of the characteristic"
      allow_nil? true
      public? true
    end

    attribute :type, :atom do
      description "the type of the characteristic"
      allow_nil? false
      public? true
      constraints one_of: [:instance, :feature, :relationship]
    end

    create_timestamp :inserted_at

    update_timestamp :updated_at
  end

  relationships do
    belongs_to :feature, Diffo.Provider.Feature do
      allow_nil? true
      public? true
    end

    belongs_to :instance, Diffo.Provider.Instance do
      allow_nil? true
      public? true
    end

    belongs_to :relationship, Diffo.Provider.Relationship do
      allow_nil? true
      public? true
    end
  end

  actions do
    defaults [:read, :destroy]

    create :create do
      description "creates a characteristic"
      accept [:feature_id, :instance_id, :relationship_id, :name, :value, :type]
      change load [:type]

      touches_resources [
        Diffo.Provider.Feature,
        Diffo.Provider.Instance,
        Diffo.Provider.Relationship
      ]
    end

    read :find_by_name do
      description "finds characteristics by name"
      get? false

      argument :query, :ci_string do
        description "Return only characteristics with names including the given value."
      end

      filter expr(contains(name, ^arg(:query)))
    end

    read :list do
      description "lists all characteristics"
    end

    read :list_characteristics_by_related_id do
      description "lists characteristics by related id and type"
      argument :related_id, :uuid
      argument :type, :atom

      filter expr(
               (relationship_id == ^arg(:related_id) or instance_id == ^arg(:related_id) or
                  feature_id == ^arg(:related_id)) and type == ^arg(:type)
             )
    end

    update :update do
      description "updates the characteristic value"
      accept [:value]
    end
  end

  identities do
    identity :feature_characteristic_uniqueness, [:feature_id, :name] do
      message "another feature characteristic exists with same name"
    end

    identity :instance_characteristic_uniqueness, [:instance_id, :name] do
      message "another instance characteristic exists with same name"
    end

    identity :relationship_characteristic_uniqueness, [:relationship_id, :type, :name] do
      message "another relationship characteristic exists with same name and direction"
    end
  end

  preparations do
    prepare build(sort: [name: :asc])
  end

  validations do
    validate present(:feature_id) do
      where one_of(:type, [:feature])
      message "feature_id must be supplied"
    end

    validate absent(:feature_id) do
      where negate(one_of(:type, [:feature]))
      message "feature_id must not be supplied"
    end

    validate present(:instance_id) do
      where one_of(:type, [:instance])
      message "instance_id must be supplied"
    end

    validate absent(:instance_id) do
      where negate(one_of(:type, [:instance]))
      message "instance_id must not be supplied"
    end

    validate present(:relationship_id) do
      where one_of(:type, [:relationship])
      message "relationship_id must be supplied"
    end

    validate absent(:relationship_id) do
      where negate(one_of(:type, [:relationship]))
      message "relationship_id must not be supplied"
    end
  end

  @doc """
  Compares two characteristic, by ascending name
  ## Examples
    iex> Diffo.Provider.Characteristic.compare(%{name: "a"}, %{name: "a"})
    :eq
    iex> Diffo.Provider.Characteristic.compare(%{name: "b"}, %{name: "a"})
    :gt
    iex> Diffo.Provider.Characteristic.compare(%{name: "a"}, %{name: "b"})
    :lt

  """
  def compare(%{name: name0}, %{name: name1}), do: Diffo.Util.compare(name0, name1)
end
