defmodule Diffo.Provider.PartyRef do
  @moduledoc """
  Diffo - TMF Service and Resource Management with a difference
  Copyright Matt Beanland beanland@live.com.au

  PartyRef - Ash Resource for a TMF PartyRef
  """
  use Ash.Resource,
    otp_app: :diffo,
    domain: Diffo.Provider,
    data_layer: AshNeo4j.DataLayer,
    extensions: [AshOutstanding.Resource, AshJason.Resource]

  neo4j do
    label(:PartyRef)

    relate([
      {:instance, :RELATES_HOW, :incoming},
      {:party, :RELATED_HOW, :outgoing}
    ])

    translate(id: :uuid)
  end

  jason do
    pick([:href, :name, :role, :referredType, :type])

    customize(fn result, record ->
      result
      |> id(record)
    end)

    rename(referredType: "@referredType", type: "@type")
    order([:id, :href, :name, :role, "@referredType", "@type"])
  end

  outstanding do
    expect([:party_id, :name, :role, :referredType, :type])
  end

  attributes do
    uuid_primary_key :id do
      description "a uuid4, unique to this party ref, generated by default"
      public? false
    end

    attribute :role, :atom do
      description "the role of the party to the instance"
      allow_nil? true
      public? true
    end

    create_timestamp :inserted_at

    update_timestamp :updated_at
  end

  relationships do
    belongs_to :instance, Diffo.Provider.Instance do
      allow_nil? false
      public? true
    end

    belongs_to :party, Diffo.Provider.Party do
      attribute_type :string
      allow_nil? false
      public? true
    end
  end

  actions do
    defaults [:read, :destroy]

    create :create do
      description "creates a party ref, relating an instance and a party"
      accept [:role]

      argument :instance_id, :uuid
      argument :party_id, :string

      change manage_relationship(:instance_id, :instance, type: :append_and_remove)
      change manage_relationship(:party_id, :party, type: :append_and_remove)

      change load [:href, :name, :referredType, :type, :party, :instance]
    end

    read :list do
      description "lists all party refs"
    end

    read :list_party_refs_by_instance_id do
      description "lists party refs by instance id"
      argument :instance_id, :uuid
      filter expr(instance_id == ^arg(:instance_id))
    end

    read :list_party_refs_by_party_id do
      description "lists party refs by party id"
      argument :party_id, :string
      filter expr(party_id == ^arg(:party_id))
    end

    read :find_by_party_id do
      description "finds party refs by party id"
      get? false

      argument :query, :ci_string do
        description "Return only party refs with party_ids including the given value."
      end

      filter expr(contains(party_id, ^arg(:query)))
    end

    update :update do
      description "updates the party ref role"
      accept [:role]
    end
  end

  identities do
    identity :instance_party_uniqueness, [:instance_id, :party_id] do
      message "another party ref exists"
    end
  end

  preparations do
    prepare build(
              load: [:href, :name, :referredType, :type, :instance],
              sort: [inserted_at: :desc]
            )
  end

  calculations do
    calculate :href, :string, expr(party.href) do
      description "the href of the related party instance"
    end

    calculate :name, :string, expr(party.name) do
      description "the name of the related party instance"
    end

    calculate :referredType, :string, expr(party.referredType) do
      description "the referredType of the related party instance"
    end

    calculate :type, :string, expr(party.type) do
      description "the type of the related party instance"
    end
  end

  @doc """
  Assists in encoding party ref id
  """
  def id(result, record) do
    party = Map.get(record, :party)

    if is_struct(party, Diffo.Provider.Party) do
      id = Map.get(party, :id)

      if id != nil do
        result |> Diffo.Util.set(:id, id)
      else
        result
      end
    else
      result
    end
  end

  @doc """
  Compares two party ref, by ascending party_id
  ## Examples
    iex> Diffo.Provider.PartyRef.compare(%{party_id: "a"}, %{party_id: "a"})
    :eq
    iex> Diffo.Provider.PartyRef.compare(%{party_id: "b"}, %{party_id: "a"})
    :gt
    iex> Diffo.Provider.PartyRef.compare(%{party_id: "a"}, %{party_id: "b"})
    :lt

  """
  def compare(%{party_id: party_id0}, %{party_id: party_id1}),
    do: Diffo.Util.compare(party_id0, party_id1)
end
