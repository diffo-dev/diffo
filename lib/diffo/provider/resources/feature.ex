defmodule Diffo.Provider.Feature do
  @moduledoc """
  Diffo - TMF Service and Resource Management with a difference
  Copyright Matt Beanland beanland@live.com.au

  Feature - Ash Resource for a TMF Feature
  """
  use Ash.Resource,
    otp_app: :diffo,
    domain: Diffo.Provider,
    data_layer: AshNeo4j.DataLayer,
    extensions: [AshJason.Resource]

  neo4j do
    label(:Feature)

    relate([
      {:instance, :DEFINES, :outgoing},
      {:characteristics, :DEFINES, :incoming}
    ])

    guard([
      {:DEFINES, :outgoing, :Instance}
    ])

    translate(id: :uuid)

    skip([:instance_id])
  end

  jason do
    pick([:name, :isEnabled, :characteristics])

    customize(fn result, _record ->
      result
      |> Diffo.Util.suppress_rename(:characteristics, :featureCharacteristic)
    end)
  end

  attributes do
    uuid_primary_key :id do
      description "a uuid4, unique to this feature, generated by default"
      public? false
    end

    attribute :name, :atom do
      description "the name of this feature"
      allow_nil? false
      public? true
    end

    attribute :isEnabled, :boolean do
      description "indicates whether the feature is enabled"
      public? true
      default true
    end

    attribute :instance_id, :uuid do
      allow_nil? false
    end

    create_timestamp :inserted_at

    update_timestamp :updated_at
  end

  relationships do
    belongs_to :instance, Diffo.Provider.Instance do
      allow_nil? true
      public? true
    end

    has_many :characteristics, Diffo.Provider.Characteristic do
      public? true
    end
  end

  actions do
    defaults [:read, :destroy]

    create :create do
      description "creates a feature, optionally with related characteristic"
      accept [:name, :isEnabled]
      argument :characteristics, {:array, :uuid}

      change manage_relationship(:characteristics, type: :append)
    end

    read :find_by_name do
      description "finds features by name"
      get? false

      argument :query, :ci_string do
        description "Return only features with names including the given value."
      end

      filter expr(contains(name, ^arg(:query)))
    end

    read :list do
      description "lists all features"
    end

    update :update do
      description "updates the feature"
      accept [:isEnabled]
    end

    update :unrelate do
      description "unrelates the feature from the instance"
      primary? true
      argument :instance, :uuid
      change manage_relationship(:instance, type: :remove)
    end

    update :relate_characteristics do
      description "relates characteristics to the feature"
      argument :characteristics, {:array, :uuid}
      change manage_relationship(:characteristics, type: :append)
    end

    update :unrelate_characteristics do
      description "unrelates characteristics from the feature"
      argument :characteristics, {:array, :uuid}
      change manage_relationship(:characteristics, type: :remove)
    end
  end

  preparations do
    prepare build(load: [:characteristics], sort: [name: :asc])
  end

  @doc """
  Compares two feature, by ascending name
  ## Examples
    iex> Diffo.Provider.Feature.compare(%{name: "a"}, %{name: "a"})
    :eq
    iex> Diffo.Provider.Feature.compare(%{name: "b"}, %{name: "a"})
    :gt
    iex> Diffo.Provider.Feature.compare(%{name: "a"}, %{name: "b"})
    :lt

  """
  def compare(%{name: name0}, %{name: name1}), do: Diffo.Util.compare(name0, name1)
end
