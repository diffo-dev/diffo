defmodule Diffo.Provider.Feature do
  @moduledoc """
  Diffo - TMF Service and Resource Management with a difference
  Copyright Matt Beanland beanland@live.com.au

  Feature - Ash Resource for a TMF Feature
  """
  use Ash.Resource,
    otp_app: :diffo,
    domain: Diffo.Provider,
    data_layer: AshNeo4j.DataLayer,
    extensions: [AshJason.Resource]

  neo4j do
    label(:Feature)

    relate([
      {:instance, :DEFINES, :outgoing},
      {:featureCharacteristic, :DEFINES, :incoming}
    ])

    guard([
      {:DEFINES, :incoming, :Characteristic}
    ])

    translate(id: :uuid)
  end

  jason do
    pick([:name, :isEnabled, :featureCharacteristic])

    customize(fn result, _record ->
      result
      |> Diffo.Util.suppress(:featureCharacteristic)
    end)
  end

  attributes do
    uuid_primary_key :id do
      description "a uuid4, unique to this feature, generated by default"
      public? false
    end

    attribute :name, :atom do
      description "the name of this feature"
      allow_nil? false
      public? true
    end

    attribute :isEnabled, :boolean do
      description "indicates whether the feature is enabled"
      public? true
      default true
    end

    create_timestamp :inserted_at

    update_timestamp :updated_at
  end

  relationships do
    belongs_to :instance, Diffo.Provider.Instance do
      allow_nil? false
      public? true
    end

    has_many :featureCharacteristic, Diffo.Provider.Characteristic do
      public? true
    end
  end

  actions do
    defaults [:read, :destroy]

    create :create do
      description "creates a feature, related to an instance"
      accept [:name, :isEnabled]
      argument :instance_id, :uuid

      change manage_relationship(:instance_id, :instance, type: :append_and_remove)
    end

    read :find_by_name do
      description "finds features by name"
      get? false

      argument :query, :ci_string do
        description "Return only features with names including the given value."
      end

      filter expr(contains(name, ^arg(:query)))
    end

    read :list do
      description "lists all features"
    end

    read :list_features_by_related_id do
      description "lists features by related id"
      argument :related_id, :uuid
      filter expr(instance_id == ^arg(:related_id))
    end

    update :update do
      description "updates the feature isEnabled"
      accept [:isEnabled]
    end
  end

  identities do
    identity :instance_feature_uniqueness, [:instance_id, :name] do
      message "another instance feature exists with same name"
    end
  end

  preparations do
    prepare build(load: [:featureCharacteristic], sort: [name: :asc])
  end

  @doc """
  Compares two feature, by ascending name
  ## Examples
    iex> Diffo.Provider.Feature.compare(%{name: "a"}, %{name: "a"})
    :eq
    iex> Diffo.Provider.Feature.compare(%{name: "b"}, %{name: "a"})
    :gt
    iex> Diffo.Provider.Feature.compare(%{name: "a"}, %{name: "b"})
    :lt

  """
  def compare(%{name: name0}, %{name: name1}), do: Diffo.Util.compare(name0, name1)
end
